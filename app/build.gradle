apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}

def gitCommitCount = 100 + Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    signingConfigs {
        release_key {
            storeFile file('../keystore/vntv_release_keystore.jks')
            keyAlias 'VNTV_RELEASE_KEYSTORE'
            keyPassword 'htsi.release'
            storePassword 'htsi.release'
        }
        debug_key {
            storeFile file('../keystore/vntv_debug_keystore.jks')
            keyAlias 'VNTV_DEBUG_KEYSTORE'
            keyPassword 'htsi.dev'
            storePassword 'htsi.dev'
        }
    }

    defaultConfig {
        applicationId "com.htsi.vntv"
        minSdkVersion 21
        targetSdkVersion 24

        archivesBaseName = "VNTV"
        versionCode gitCommitCount
        signingConfig signingConfigs.debug_key
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BASE_URL", '"https://api.htvonline.com.vn"'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release_key
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug_key
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent, output.outputFile.name.
                    replace(".apk", "_" + versionName + "_" + versionCode + "_" + getDate() + ".apk"));
        }
    }
}

ext {
    // Android Support AppCompat
    supportLibVersion = '24.2.1'
    // Butter Knife
    butterknifeLibVersion = '8.1.0'
    // Dagger
    daggerLibVersion = '2.7'
    // HTTP Connection
    retrofit2LibVersion = '2.1.0'
    okhttpLoggingInterceptorLibVersion = '3.3.1'
    // Json
    gsonLibVersion = '2.7'
    // Image
    glideLibVersion = '3.7.0'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile project(':vitamio')

    // Android Support Libs
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:palette-v7:${supportLibVersion}"

    // Butter-knife
    compile "com.jakewharton:butterknife:${butterknifeLibVersion}"
    apt "com.jakewharton:butterknife-compiler:${butterknifeLibVersion}"

    // Utils
    compile "com.github.bumptech.glide:glide:${glideLibVersion}"
    compile 'com.google.code.gson:gson:2.5'

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:${retrofit2LibVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofit2LibVersion}"

    // Dependence Injection
    compile "com.google.dagger:dagger:${daggerLibVersion}"
    apt "com.google.dagger:dagger-compiler:${daggerLibVersion}"
}
